singularity:
        "docker://continuumio/miniconda3:4.5.12"
SAMPLES = [
  "Col-cont-rep1","Col-cont-rep2","Col-cont-rep3",
  "Col-sub-rep1","Col-sub-rep2","Col-sub-rep3",
  "gad1CS-cont-rep1","gad1CS-cont-rep2","gad1CS-cont-rep3",
  "gad1CS-sub-rep1","gad1CS-sub-rep2","gad1CS-sub-rep3",
  "gad1245-cont-rep1","gad1245-cont-rep2","gad1245-cont-rep3",
  "gad1245-sub-rep1","gad1245-sub-rep2","gad1245-sub-rep3",
  "gad1SALK-cont-rep1","gad1SALK-cont-rep2","gad1SALK-cont-rep3",
  "gad1SALK-sub-rep1","gad1SALK-sub-rep2","gad1SALK-sub-rep3",
  "gad21-cont-rep1","gad21-cont-rep2","gad21-cont-rep3",
  "gad21-sub-rep1","gad21-sub-rep2","gad21-sub-rep3",
  "gad2OEB33-cont-rep1","gad2OEB33-cont-rep2","gad2OEB33-cont-rep3",
  "gad2OEB33-sub-rep1","gad2OEB33-sub-rep2","gad2OEB33-sub-rep3",
  "pop28-cont-rep1","pop28-cont-rep2","pop28-cont-rep3",
  "pop28-sub-rep1","pop28-sub-rep2","pop28-sub-rep3",
]
FASTA = "TAIR10_chr_all.fas"
STAR_INDEX_FILES = ["chrLength.txt","chrNameLength.txt","chrName.txt",
"chrStart.txt","SA","SAindex","Genome","genomeParameters.txt"]
MAX_THREADS = 32

LOCAL_SCRATCH = "./local_scrach"

outFilterMismatchNoverLmax = 0.02
alignEndsType              = "Local"

GTF = "Araport11_GFF3_genes_transposons.201606_noChr.gtf"
############################
# Include other Snakefiles #
############################
include:
	"rules/misc.smk"

#######################################
# Convienient rules to define targets #
#######################################
localrules:
	all

rule all:
	input:
		expand("../0_fastq/submergence_batch1/{prefix}.fastq.gz", prefix=SAMPLES),
		expand("../2_trimmed/{prefix}_Trimmed.fastq.gz", prefix=SAMPLES),
		expand("../1_FastQC/Raw/{prefix}_fastqc.html", prefix=SAMPLES),
		expand("../1_FastQC/Trimmed/{prefix}_Trimmed_fastqc.html",prefix=SAMPLES),
		expand("../At_genome/{fasta}",fasta=FASTA),
		expand("../At_genome/{fasta}.fai",fasta=FASTA),
		expand("../At_genome/{fasta}.gz_STAR_index/NumberOfReferences",fasta=FASTA),
		expand("../At_genome/{fasta}.gz_STAR_index/GenomeLength",fasta=FASTA),
		expand("../At_genome/{fasta}.gz_STAR_index/{file}",fasta=FASTA,file=STAR_INDEX_FILES),
		expand("../3_BAM/{prefix}.STARAligned_TAIR10.sortedByCoord.bam",prefix=SAMPLES),
		expand("../3_BAM/{prefix}.SJ.out.tab",prefix=SAMPLES),
		expand("../3_BAM/log_files/{prefix}.Log.out",prefix=SAMPLES),
		expand("../3_BAM/log_files/{prefix}.Log.progress.out",prefix=SAMPLES),
		expand("../3_BAM/log_files/{prefix}.Log.final.out",prefix=SAMPLES),
		expand("../3_BAM/log_files/{prefix}.Log.std.out",prefix=SAMPLES),
		expand("../At_genome/{gtf}",gtf=GTF),
		expand("../4_Counts/Ying_Submergence_batch1.txt", prefix=SAMPLES)

################
# Rules Proper #
################

rule fastqc_raw:
	input:
		"../0_fastq/submergence_batch1/{prefix}.fastq.gz"
	output:
		zip = "../1_FastQC/Raw/{prefix}_fastqc.zip",
		html = "../1_FastQC/Raw/{prefix}_fastqc.html",
	conda:
		"envs/default.yml"
	shell:
		"""
		fastqc --threads 1 {input}
		mv ../0_fastq/submergence_batch1/{wildcards.prefix}_fastqc.zip {output.zip}
		mv ../0_fastq/submergence_batch1/{wildcards.prefix}_fastqc.html {output.html}
		"""

rule Trimmatic:
	input:
		r1 = "../0_fastq/submergence_batch1/{prefix}.fastq.gz",
		adaptors = "misc/trimmomatic_adapters/TruSeq3-SE.fa",
	output:
		r1 = "../2_trimmed/{prefix}_Trimmed.fastq.gz",
	conda:
		"envs/default.yml"
	shell:
		"""
		trimmomatic SE \
		      -threads 1 \
		      {input.r1} {output.r1} \
		      ILLUMINACLIP:{input.adaptors}:2:30:10:3:true \
		      LEADING:2 \
		      TRAILING:2 \
		      SLIDINGWINDOW:4:15 \
		      MINLEN:70
		"""

rule fastqc_trimmed:
	input:
		"../2_trimmed/{prefix}_Trimmed.fastq.gz",
	output:
		zip  = "../1_FastQC/Trimmed/{prefix}_Trimmed_fastqc.zip",
		html = "../1_FastQC/Trimmed/{prefix}_Trimmed_fastqc.html",
	conda:
		"envs/default.yml"
	shell:
		"""
		fastqc --threads 1 {input}
		mv ../2_trimmed/{wildcards.prefix}_Trimmed_fastqc.zip {output.zip}
		mv ../2_trimmed/{wildcards.prefix}_Trimmen_fastqc.html {output.html}
		"""

rule STAR_NumberOfReferences:
	input:
		"../At_genome/{fasta}.fai"
	output:
		"../At_genome/{fasta}.gz_STAR_index/NumberOfReferences"
	shell:
		"""
		wc -l < {input} > {output}
		"""

rule STAR_GenomeLength:
	input:
		"../At_genome/{fasta}.fai"
	output:
		"../At_genome/{fasta}.gz_STAR_index/GenomeLength"
	shell:
		"""
		awk '{{tot+=$2}}END{{print tot}}' {input} > {output}
		"""

rule STAR_indexing:
	input:
		ref                = "../At_genome/{fasta}",
		NumberOfReferences = "../At_genome/{fasta}.gz_STAR_index/NumberOfReferences",
		GenomeLength       = "../At_genome/{fasta}.gz_STAR_index/GenomeLength",
	output:
		expand("../At_genome/{{fasta}}.gz_STAR_index/{file}", file=STAR_INDEX_FILES)
	threads:
		MAX_THREADS
	resources:
		mem_gb = 4
	params:
		tmp_dir = LOCAL_SCRATCH,
		limitGenomeGenerateRAM = 320 * 1000000000
	conda:
		"envs/default.yml"
	shell:
		"""
		TMP_OUT="$(mktemp --directory --dry-run {params.tmp_dir}/tmp.XXXXXXXXXX_STARtmp)"
		TMP_GENOME_OUT="$(mktemp --directory {params.tmp_dir}/tmp.XXXXXXXXXX_STAR_index)"

		function clean_up {{
		  # Perform program exit housekeeping
		  if [ ! -z "${{TMP_OUT}}" ] && [ -e "${{TMP_OUT}}" ]; then
		    rm -rf "${{TMP_OUT}}" "${{TMP_GENOME_OUT}}"
		  fi
		  trap 0  # reset to default action
		  exit
		}}
		trap clean_up 0 1 2 3 15 #see 'man signal' for descriptions http://man7.org/linux/man-pages/man7/signal.7.html

		genomeChrBinNbits=$(echo "v=l($(cat {input.GenomeLength})/$(cat {input.NumberOfReferences})) / l(2); scale=0; def=18; if (def<v) def else v/1" | bc -l)
		genomeSAindexNbases=$(echo "v=(l($(cat {input.GenomeLength}))/2-1) / l(2); def=14; if(def<v) def else v/1; scale=0;" | bc -l)

		STAR \
		  --runMode genomeGenerate \
		  --outTmpDir ${{TMP_OUT}} \
		  --genomeDir ${{TMP_GENOME_OUT}} \
		  --genomeChrBinNbits ${{genomeChrBinNbits}} \
		  --genomeSAindexNbases ${{genomeSAindexNbases}} \
		  --genomeFastaFiles {input.ref} \
		  --limitGenomeGenerateRAM {params.limitGenomeGenerateRAM} \
		  --runThreadN {threads}

		mv ${{TMP_GENOME_OUT}}/* ../At_genome/{wildcards.fasta}.gz_STAR_index/
		"""

rule STAR_mapping_SE:
	input:  
		SE    = "../2_trimmed/{prefix}_Trimmed.fastq.gz",
		index = expand("../At_genome/{fasta}.gz_STAR_index/{file}",fasta=FASTA,file=STAR_INDEX_FILES),
	output: 
		bam              = "../3_BAM/{prefix}.STARAligned_TAIR10.sortedByCoord.bam",
		splice_junctions = "../3_BAM/{prefix}.SJ.out.tab"
	log:
		main     = "../3_BAM/log_files/{prefix}.Log.out",
		progress = "../3_BAM/log_files/{prefix}.Log.progress.out",
		final    = "../3_BAM/log_files/{prefix}.Log.final.out",
		std      = "../3_BAM/log_files/{prefix}.Log.std.out",
#	wildcard_constraints:
#		sample  = "[^\/]+",
#		dataset = "[^\/]+",
#	resources:
#		mem_mb = lambda wildcards, threads: threads * 2
#		mem_gb = lambda wildcards, threads, input: math.ceil( (sum(os.path.getsize(f) for f in input['index']) / 1024**3) + (0.15 * threads) ) + 5
#		mem_gb = lambda wildcards, input: math.ceil( (sum(os.path.getsize(f) for f in input['index'] if os.path.isfile(f)) / 1024**3) + (0.15 * THREADS_MAX) ) + 5,
#		time_min = lambda wildcards, attempt, input: max(math.ceil(sum(os.path.getsize(f) for f in [input['SE']] if os.path.isfile(f)) / 1024**3 / 30), 15) * 10 * attempt
#		time_min = lambda wildcards, attempt: 30 * attempt
	conda:
		"envs/default.yml"
	threads:
		MAX_THREADS
	params:
		tmp_dir                     = LOCAL_SCRATCH,
		outBAMsortingThreadN        = 20,
		limitIObufferSize           = 150 * 1000000,
		limitBAMsortRAM             = 190 * 1000000000,
		outFileNamePrefix           = lambda wildcards, output: output['bam'].replace('STARAligned_TAIR10.sortedByCoord.bam', ''),
		outFilterMultimapNmax       = 5,
		alignEndsType               = alignEndsType,
		outFilterMismatchNoverLmax  = outFilterMismatchNoverLmax,
		outFilterMatchNminOverLread = 1.00 - outFilterMismatchNoverLmax,
		alignIntronMax              = 10000,
		alignMatesGapMax            = 10000,
		genomeLoad                  = 'NoSharedMemory',
		fasta                       = FASTA
	shell:
		"""
		TMP_OUT="$(mktemp --directory --dry-run {params.tmp_dir}/tmp.XXXXXXXXXX_STARtmp)"

		function clean_up {{
		  # Perform program exit housekeeping
		  if [ ! -z "${{TMP_OUT}}" ] && [ -e "${{TMP_OUT}}" ]; then
		    rm -rf "${{TMP_OUT}}"
		  fi
		  trap 0  # reset to default action
		  exit
		}}
		trap clean_up 0 1 2 3 15 #see 'man signal' for descriptions http://man7.org/linux/man-pages/man7/signal.7.html

		STAR \
		  --runMode alignReads \
		  --outStd BAM_SortedByCoordinate \
		  --outBAMcompression 0 \
		  --outTmpDir ${{TMP_OUT}}/ \
		  --genomeDir ../At_genome/{params.fasta}.gz_STAR_index \
		  --genomeLoad {params.genomeLoad} \
		  --runThreadN {threads} \
		  --outBAMsortingThreadN {params.outBAMsortingThreadN} \
		  --limitIObufferSize {params.limitIObufferSize} \
		  --limitBAMsortRAM {params.limitBAMsortRAM} \
		  --readFilesIn {input.SE} \
		  --readFilesCommand pigz -dcp2 \
		  --outFileNamePrefix {params.outFileNamePrefix} \
		  --outSAMtype BAM SortedByCoordinate \
		  --outFilterMultimapScoreRange 0 \
		  --outFilterMultimapNmax {params.outFilterMultimapNmax} \
		  --outFilterMismatchNoverLmax {params.outFilterMismatchNoverLmax} \
		  --outFilterMatchNminOverLread {params.outFilterMatchNminOverLread} \
		  --outSJfilterOverhangMin 35 20 20 20 \
		  --outSJfilterCountTotalMin 10 3 3 3 \
		  --outSJfilterCountUniqueMin 5 1 1 1 \
		  --alignEndsType {params.alignEndsType} \
		  --alignSoftClipAtReferenceEnds No \
		  --outSAMstrandField intronMotif \
		  --outSAMattributes All \
		  --alignIntronMax {params.alignIntronMax} \
		  --alignMatesGapMax {params.alignMatesGapMax} \
		  --outSAMattrRGline ID:{output[0]} PL:Illumina PU:Unknown LB:Unknown SM:{wildcards.prefix} \
		| samtools calmd -b --threads {threads} /dev/stdin ../At_genome/{params.fasta} \
		> {output.bam}

		samtools quickcheck -v {output.bam}
		"""

rule FeatureCounts_gene:
	input:
		bam = expand("../3_BAM/{prefix}.STARAligned_TAIR10.sortedByCoord.bam",prefix=set(SAMPLES)),
		gtf = "../At_genome/Araport11_GFF3_genes_transposons.201606_noChr.gtf"
	output:
		counts = "../4_Counts/Ying_Submergence_batch1.txt",
		summary = "../4_Counts/Ying_Submergence_batch1.txt.summary"
	conda:
		"envs/default.yml"
	shell:
		"""
		featureCounts -T 4 -a {input.gtf} -o {output.counts} {input.bam}
		"""
